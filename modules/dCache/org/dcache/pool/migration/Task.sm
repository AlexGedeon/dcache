%{
// 
%}

%class Task
%package org.dcache.pool.migration
%access package
%import diskCacheV111.vehicles.*

%start TASK::Queued
%map TASK
%%
Queued
{
        run
                GettingLocations
                {
                }
}

GettingLocations
Entry
{       
        queryLocations();
}
{
        timeout
                Failed
                {
                        fail("PnfsManager failed (no response)");
                }
        noroute
                Failed
                {
                        fail("PnfsManager failed (no route to cell)");
                }
        failure(rc: int, cause: Object)
                Failed
                {
                        fail("PnfsManager failed (" + cause + ")");
                }
        cancel
                Cancelled
                {
                }
        success
                [ ctxt.hasMoreLocations() ]
                UpdatingExistingFile
                {
                        updateExistingReplica();
                }
        success
                InitiatingCopy
                {
                }
}

// We got existing copies. Try to update one of them. Keep
// trying until we succeed or we tried them all.
UpdatingExistingFile
{
        timeout
                [ ctxt.hasMoreLocations() ]
                nil
                {
                        updateExistingReplica();
                }
        timeout
                [ ctxt.isEager() ]
                InitiatingCopy
                {
                }
        timeout
                Failed
                {
                        fail("Remote pool failed (no response)");
                }
        noroute
                [ ctxt.hasMoreLocations() ]
                nil
                {
                        updateExistingReplica();
                }
        noroute
                [ ctxt.isEager() ]
                InitiatingCopy
                {
                }
        noroute
                Failed
                {
                        fail("Remote pool failed (no route to cell)");
                }
        failure(rc: int, cause: Object)
                [ ctxt.hasMoreLocations() ]
                nil
                {
                        updateExistingReplica();
                }
        failure(rc: int, cause: Object)
                InitiatingCopy
                {
                }
        success
                [ ctxt.getMustMovePins() ]
                MovingPin
                {
                }
        success
                Done
                {
                }
        cancel
                Cancelled
                {
                }
}

// Update got cancelled. We simply wait for the previous update 
// operation to finish and then go to either Cancelled, Done or
// Failed.
CancellingUpdate
{
        timeout
                Cancelled
                {
                }
        noroute
                Cancelled
                {
                }
        failure(rc: int, cause: Object)
                Cancelled
                {
                }
        success
                [ ctxt.getMustMovePins() ]
                Failed
                {
                        fail("Failed to cancel task");
                }
        success
                Done
                {
                }
        cancel
                nil
                {
                }
}

// Ask pool to copy the file.
InitiatingCopy
Entry
{
        initiateCopy();
}
{
        success
                Copying
                {
                }
        nopools
                Failed
                {
                        fail(String.format("No targets"));
                }
        noroute
                Failed
                {
                        fail(String.format("Pool %s failed (no route to cell)",
                                           ctxt.getTarget()));
                }
        failure(rc: int, cause: Object)
                Failed
                {
                        fail(String.format("Pool %s failed (%s)",
                                           ctxt.getTarget(), cause));
                }
        timeout
                Waiting
                {
                        // No reply, but message could have been 
                        // received anyway, so try to cancel it.
                        cancelCopy();
                }
        cancel
                Cancelling
                {
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ message.getReturnCode() != 0 ]
                Failed
                {
                        fail(String.format("Transfer to %s failed (%s)",
                                           ctxt.getTarget(), 
                                           message.getErrorObject()));
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ message.getReturnCode() == 0 && ctxt.getMustMovePins() ]
                MovingPin
                {
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ message.getReturnCode() == 0 ]
                Done
                {
                }
}

// Pool accepted the task. Wait for it to finish and 
// periodically send a ping to verify it is alive.
Copying
Entry
{
        startTimer(ctxt.getPingPeriod());
}
Exit
{
        stopTimer();
}
{                
        timer
                Pinging
                {
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ message.getReturnCode() != 0 ]
                Failed
                {
                        fail(String.format("Transfer to %s failed (%s)",
                                           ctxt.getTarget(), 
                                           message.getErrorObject()));
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ message.getReturnCode() == 0 && ctxt.getMustMovePins() ]
                MovingPin
                {
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ message.getReturnCode() == 0 ]
                Done
                {
                }
        cancel
                Cancelling
                {
                }
}

// Send a ping.
Pinging
Entry
{
        ping();
}
{                
        success
                Copying
                {
                        // Task is alive
                }
        failure(rc: int, cause: Object)
                Waiting
                {
                        // Task is gone
                }
        noroute
                NoResponse
                {
                        // We cannot talk to the pool
                }
        timeout
                NoResponse
                {
                        // Pool does not respond.
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ message.getReturnCode() != 0 ]
                Failed
                {
                        fail(String.format("Transfer to %s failed (%s)",
                                           ctxt.getTarget(), 
                                           message.getErrorObject()));
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ message.getReturnCode() == 0 && ctxt.getMustMovePins() ]
                MovingPin
                {
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ message.getReturnCode() == 0 ]
                Done
                {
                }
        cancel
                Cancelling
                {
                }
}

// Pool did not reply to ping. Let's give it some time
// and ping it again. 
NoResponse
Entry
{
        startTimer(ctxt.getNoResponseTimeout());
}
Exit
{       
        stopTimer();
}
{                
        timer
                nil
                {
                        ping();
                }
        success
                Copying
                {
                        // Task is alive
                }
        failure(rc: int, cause: Object)
                Waiting
                {
                        // Task is gone
                }
        noroute
                Failed
                {
                        // We still cannot talk to the pool
                        fail(String.format("Pool %s failed (no route to cell)",
                                           ctxt.getTarget()));
                }
        timeout
                Failed
                {
                        // Message timed out again
                        fail(String.format("Pool %s failed (no response)",
                                           ctxt.getTarget()));
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ message.getReturnCode() != 0 ]
                Failed
                {
                        fail(String.format("Transfer to %s failed (%s)",
                                           ctxt.getTarget(), 
                                           message.getErrorObject()));
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ message.getReturnCode() == 0 && ctxt.getMustMovePins() ]
                MovingPin
                {
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ message.getReturnCode() == 0 ]
                Done
                {
                }
        cancel
                Cancelling
                {
                }
}

// Target pool no longer has the task. Let's wait for
// the CopyFinished message.
Waiting
Entry
{
        startTimer(ctxt.getTaskDeadTimeout());
}
Entry
{
        stopTimer();
}
{                
        timer
                Failed
                {
                        fail(String.format("Pool %s failed (no response)",
                                           ctxt.getTarget()));
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ message.getReturnCode() != 0 ]
                Failed
                {
                        fail(String.format("Transfer to %s failed (%s)",
                                           ctxt.getTarget(), 
                                           message.getErrorObject()));
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ message.getReturnCode() == 0 && ctxt.getMustMovePins() ]
                MovingPin
                {
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ message.getReturnCode() == 0 ]
                Done
                {
                }
        Default
                nil
                {
                }
}

MovingPin
Entry
{
        movePin();
}
{
        success
                Done
                {
                }
        failure(rc: int, cause: Object)
                Failed
                {
                        fail("Pin manager failed (" + cause + ")");
                }
        noroute
                Failed
                {
                        fail("Pin manager failed (no route to cell)");
                }
        timeout
                Failed
                {
                        fail("Pin manager failed (timeout)");
                }
        cancel
                Failed
                {
                        fail("Failed to cancel task");
                }
}

// User requested to abort the task, but we already asked
// a pool to copy the file. Let's try to cancel the copy 
// and wait for CopyFinished. 
Cancelling
Entry
{
        cancelCopy();
        startTimer(ctxt.getTaskDeadTimeout());
}
Exit
{
        stopTimer();
}
{
        timer
                Failed
                {
                        fail(String.format("Pool %s failed (no response)",
                                           ctxt.getTarget()));
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ message.getReturnCode() != 0 ]
                Cancelled
                {
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ message.getReturnCode() == 0 && ctxt.getMustMovePins() ]
                Failed
                {
                        fail("Failed to cancel task");
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ message.getReturnCode() == 0 ]
                Done
                {
                }
        Default
                nil
                {
                }
}

// Successfully cancelled task. We got confirmation that the
// transfer did not go through
Cancelled
Entry
{
        notifyCancelled();
}
{
        Default
                nil
                {
                }
}

// Something went wrong. In some cases the transfer could have
// succeeded, but we don't know.
Failed
{
        Default
                nil
                {
                }   
}

// Transfer completed.
Done
Entry
{
        notifyCompleted();
}
{    
        Default
                nil
                {
                }   
}

%%
