%{
// PinMover task for the pin manager
%}


%class PinMover
%package org.dcache.services.pinmanager1
%access package
%import diskCacheV111.vehicles.*
%import diskCacheV111.util.CacheException

%start PinMoverFSM::Init
%map PinMoverFSM
%%
Init
{
        go
                MarkingSticky
                {
                }
}


MarkingSticky
Entry
{
        markSticky();
}
{
        timeout
                Done
                {
                        fail("pool timeout");
                }
        success(message: PoolSetStickyMessage)
                [ ctxt.pinMoveSucceed() ]
                UnsettingStickyFlags
                {
                }
        success(message: PoolSetStickyMessage)
                Done
                {
                }
        failure(rc: int, error: Object)
                Done
                {
                        fail(error);
                }
        noroute
                Done
                {
                        fail("No route to pool");
                }
}

UnsettingStickyFlags
Entry
{
        unsetStickyFlags();
}
{
        unsetStickyFlagMessagesSent
                Done
                {
                   succeed();
                }

        timeout
                nil
                {
                        fail("unset sticky flag timeout");
                }

        noroute
                Done
                {
                        fail("No route to pool");
                }

        success(message: PoolSetStickyMessage)
                Done
                {
                        succeed();
                }

        failure(rc: int, error: Object)
                [ rc == CacheException.FILE_NOT_IN_REPOSITORY ]
                Done
                {
                        succeed();
                }

        failure(rc: int, error: Object)
                Done
                {
                        fail(error);
                }
}

Done
{
}

Default
{
        failure(rc: int, error: Object)
                Done
                {
                        fail(error);
                }
        noroute
                Done
                {
                        fail("Communication failure");
                }
        timeout
                Done
                {
                        fail("Communication timeout");
                }
        success(message: PoolSetStickyMessage)
                Done
                {
                        fail("Unexpected answer received in state "
                             + context.getState().getName()
                             + ": " + context.getTransition());
                }
}


%%
